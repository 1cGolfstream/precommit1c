#Использовать tempfiles
#Использовать asserts

Перем юТест;

Процедура Инициализация()
		
КонецПроцедуры

Функция ПолучитьСписокТестов(Знач Контекст) Экспорт
	
	юТест = Контекст;
	
	ВсеТесты = Новый Массив;
	
	ВсеТесты.Добавить("Тест_ДолженРазложитьФайлОбработкиИзЗаданнойПапки");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура ПослеЗапускаТеста() Экспорт
	//ВременныеФайлы.Удалить();
КонецПроцедуры

Процедура Тест_ДолженРазложитьФайлОбработкиИзЗаданнойПапки() Экспорт

	ИмяКаталога = ТекущийСценарий().Каталог;
	ИмяФайла = "Fixture";
	ИмяФайлаСРасширением = ИмяФайла+".epf";
	
	ПутьФайла = ОбъединитьПути(ИмяКаталога, ИмяФайлаСРасширением);
	Ожидаем.Что(Новый Файл(ПутьФайла).Существует(), "Исходный файл должен существовать").ЭтоИстина();
	
	КаталогВыгрузки = ВременныеФайлы.СоздатьКаталог();
	
	Исполнитель = ЗагрузитьСценарий(ОбъединитьПути(ИмяКаталога,"../v8files-extractor.os"));
	Лог = Логирование.ПолучитьЛог("oscript.app.v8files-extractor");
	Лог.УстановитьУровень(УровниЛога.Отладка);
	
	Исполнитель.РазобратьФайл(ПутьФайла, КаталогВыгрузки);
	
	МассивФайлов = НайтиФайлы(КаталогВыгрузки, ПолучитьМаскуВсеФайлы());
	Ожидаем.Что(МассивФайлов, "В каталоге выгрузки должны быть файлы").Заполнено();
	Нашли = Ложь;
	для Каждого КаталогИсходников Из МассивФайлов Цикл
		Нашли = КаталогИсходников.Имя = ИмяФайла;
		Если Нашли Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Ожидаем.Что(Нашли, "Должны были найти имя каталога обработки ").ЭтоИстина();
	ВыгруженныеФайлы = НайтиФайлы(КаталогИсходников.ПолноеИмя, ПолучитьМаскуВсеФайлы());
	ИменаВыгруженныхФайлов = РазвернутьМассивФайловВИменаФайлов(ВыгруженныеФайлы);
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("renames.txt");
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("Form");
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("Макеты");
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("und");
КонецПроцедуры

Функция РазвернутьМассивФайловВИменаФайлов(Знач МассивОбъектовФайл)
	
	ИменаФайлов = Новый Массив;
	Для Каждого Файл Из МассивОбъектовФайл Цикл
		ИменаФайлов.Добавить(Файл.Имя);
	КонецЦикла;
	
	Возврат ИменаФайлов;
	
КонецФункции

Инициализация();
